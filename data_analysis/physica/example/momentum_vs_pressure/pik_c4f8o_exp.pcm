! pik_n.pcm
! gh   02.03.20

enable echo
clear

m_pi=0.13957
m_k=0.49368
m_p=0.93827
n0=1.00137

i=0
do j=[0.5:14.0:0.1]
  i=i+1
  p[i]=j
enddo

beta_pi=p/sqrt(p**2+m_pi**2)
beta_k=p/sqrt(p**2+m_k**2)
beta_p=p/sqrt(p**2+m_p**2)

nmin_pi=1/beta_pi-1
nmin_k=1/beta_k-1
nmin_p=1/beta_p-1

! min n for pi
press_pi=(nmin_pi)/(n0-1)

! computations for pi/K separation
n_pikm=(nmin_k-nmin_pi)+nmin_pi+1
press_pikm=(n_pikm-1)/(n0-1)

! computations for K/p separation
n_kpm=(nmin_p-nmin_k)+nmin_k+1
press_kpm=(n_kpm-1)/(n0-1)

! set limits consistent with good pi+/K+ separation
! Keep in mind that for a given SHMS Central P, we can have pi+ with P-15% 
! and K+ with P+25%
! the goal is that for these disparate momenta, the pressure is still 0.1 atm
! below the minimum pressure required for K+
! the 55% factor, below, seems to accomplish this.
!
!n_pik=(nmin_k-nmin_pi)*0.55+nmin_pi+1

pp25=p*1.25
pm15=p*0.85

beta_pi_m15=pm15/sqrt(pm15**2+m_pi**2)
beta_k_p25=pp25/sqrt(pp25**2+m_k**2)

nmin_pi_m15=1/beta_pi_m15-1
nmin_k_p25=1/beta_k_p25-1

press_pi_m15=(nmin_pi_m15)/(n0-1)
press_k_p25=(nmin_k_p25)/(n0-1)

press_pik=press_k_p25-0.1

! set limits consistent with the vapor pressure
lenp=len(p)
do  j=[1:lenp]
  if (press_pik[j]>0.95) then press_pik[j]=0.95
enddo

n_pik2=press_pik*(n0-1)+1
!n_kp2=press_kp*(n0-1)+1

! calculate the number of P.E.'s taking into account
! the instrumentation wavelength characteristics via
!   q1 +q2*lambda +q3*(lambda**2) +q4*(lambda**3) +q5*(lambda**4) +p6*(lambda**5)
! where lambda is in m from 180 to 630 nm, and
! old UV_trans with XP4508B
!q1=-0.27422
!q2= 2.51633E+06
!q3=-2.00682E+12
!q4=-3.17555E+18
!q5=-4.50149E+24
!q6= 9.97081E+30
! new UV_trans with ECI, Corning 7980, R1584
q1=2.68771E-02
q2=-2.56173E+06 
q3=2.75535E+13 
q4=-8.13560E+19
q5=9.19909E+25
q6=-3.51682E+31
! assume 1.0m mean pathlength of particle in radiator gas
! assume that 75% of the light is focussed onto the PMTs
pef=2*3.1415/137*(1-1/(beta_pi*n_pik2)**2)*1.0*0.75
l1=630.e-9
l2=180.e-9
pe_pik=pef*(-q1/l1+q2*ln(l1)+q3*l1+q4*(l1**2)/2+q5*(l1**3)/3+q6*(l1**4)/4 +q1/l2-q2*ln(l2)-q3*l2-q4*(l2**2)/2-q5*(l2**3)/3-q6*(l2**4)/4)
!pe_kp=2*3.1415/137*(1-1/(beta_k*n_kp2)**2)*(1/275.e-9 - 1/500.e-9)*0.2*0.65*0.5

! reduced but constant pressure over a larger momentum range if npe>25
p_exp=0.7
pe_pik_e=pe_pik
press_pik_e=press_pik
do j=[1:lenp]
  if (pe_pik[j]>35 & press_pik[j]>p_exp) then
     pe_pik_e[j]=pe_pik[j]*p_exp/press_pik[j]
     press_pik_e[j]=p_exp
  endif
enddo

! plot results
orientation landscape
label\xaxis `Momentum (GeV/c)'
set linthk 3
set ylog 10
set nsyinc 10
set %xlaxis 27
set nsxinc 2
set font triumf.2

window 1
label\yaxis `C<D>4<U>F<D>8<U>O Pressure (atm)'
scale 2.8 11 5 -1.3 1.2 3
set xvmin 1
set xvmax 11
col 1
graph\axes

set lintyp 5
col 4
graph\noaxes p press_kpm
set lintyp 8
col 2
graph\noaxes p press_pikm
col 3
set lintyp 9
graph\noaxes p press_pi

set lintyp 1
col 1
graph\noaxes p press_pik
col 7
graph\noaxes p press_pik_e

window 2
label\yaxis `<pi><U>+<D> Photoelectrons'
scale 2.8 11 5 0 50 5
set xvmin 1
set xvmax 11
set ylog 0
set nsyinc 5

col 1
set lintyp 1
graph\axes
col 1
graph\noaxes p pe_pik
col 7
graph\noaxes p pe_pik_e

! calculate the detection efficiency, assuming that there is an experimental
! cut at 1.5pe to eliminate knock-on events

i=0
do j=[0:30:0.1]
  i=i+1
  x[i]=j
enddo

scalar\dummy k

do i=[1:lenp]
  y=(pe_pik[i]**x)*(exp(-pe_pik[i]))/gamma(x+1)
  sumylo[i]=sum(y[k],k,1:15)/10
  sumy[i]=sum(y[k],k,1:len(y))/10
enddo
ineff_pik=1-(sumy-sumylo)/sumy*(1-exp(-pe_pik))

list p press_pik pe_pik ineff_pik

col 1
window 0
set %txthit 3.0
col 4
text `p'
col 2
text `K<U>+'
col 3
text `<pi><U>+'
col 1
text `set'
col 1
text `C<D>4<U>F<D>8<U>O'
