! pik_n.pcm
! gh   02.03.20

enable echo
clear

m_pi=0.13957
m_k=0.49368
m_p=0.93827
n0=1.00137
n0_c4f10=1.00143

i=0
do j=[0.5:14.0:0.1]
  i=i+1
  p[i]=j
enddo

beta_pi=p/sqrt(p**2+m_pi**2)
beta_k=p/sqrt(p**2+m_k**2)
beta_p=p/sqrt(p**2+m_p**2)

nmin_pi=1/beta_pi-1
nmin_k=1/beta_k-1
nmin_p=1/beta_p-1

! min n for pi
press_pi=(nmin_pi)/(n0-1)

! computations for pi/K separation
n_pikm=(nmin_k-nmin_pi)+nmin_pi+1
press_pikm=(n_pikm-1)/(n0-1)

! computations for K/p separation
n_kpm=(nmin_p-nmin_k)+nmin_k+1
press_kpm=(n_kpm-1)/(n0-1)

label\xaxis `Momentum (GeV/c)'
set linthk 3
set ylog 10
set nsyinc 10
set %xlaxis 27
set nsxinc 2
set font triumf.2

! set limits consistent with good pi+/K+ separation
! Keep in mind that for a given SHMS Central P, we can have pi+ with P-15% 
! and K+ with P+25%
! the goal is that for these disparate momenta, the pressure is still 0.1 atm
! below the minimum pressure required for K+
! the 55% factor, below, seems to accomplish this.
!
!n_pik=(nmin_k-nmin_pi)*0.55+nmin_pi+1

pp25=p*1.25
pm15=p*0.85

beta_pi_m15=pm15/sqrt(pm15**2+m_pi**2)
beta_k_p25=pp25/sqrt(pp25**2+m_k**2)

nmin_pi_m15=1/beta_pi_m15-1
nmin_k_p25=1/beta_k_p25-1

press_pi_m15=(nmin_pi_m15)/(n0-1)
press_k_p25=(nmin_k_p25)/(n0-1)
press_k_p25_c4f10=(nmin_k_p25)/(n0_c4f10-1)

press_pik=press_k_p25-0.1
press_pik_c4f10=press_k_p25_c4f10-0.1

! set limits consistent with the vapor pressure
lenp=len(p)
do  j=[1:lenp]
  if (press_pik[j]>0.95) then press_pik[j]=0.95
  if (press_pik_c4f10[j]>0.95) then press_pik_c4f10[j]=0.95
enddo

n_pik2=press_pik*(n0-1)+1
n_pik2_c4f10=press_pik_c4f10*(n0_c4f10-1)+1
!n_kp2=press_kp*(n0-1)+1

! calculate the number of P.E.'s taking into account
! the instrumentation wavelength characteristics via
!   q1 +q2*lambda +q3*(lambda**2) +q4*(lambda**3) +q5*(lambda**4) +p6*(lambda**5)
! where lambda is in m from 180 to 630 nm, and
q1=-0.27422
q2= 2.51633E+06
q3=-2.00682E+12
q4=-3.17555E+18
q5=-4.50149E+24
q6= 9.97081E+30
! assume 1.0m mean pathlength of particle in radiator gas
! assume that 75% of the light is focussed onto the PMTs
pef=2*3.1415/137*(1-1/(beta_pi*n_pik2)**2)*1.0*0.75
l1=630.e-9
l2=180.e-9
pe_pik=pef*(-q1/l1+q2*ln(l1)+q3*l1+q4*(l1**2)/2+q5*(l1**3)/3+q6*(l1**4)/4 +q1/l2-q2*ln(l2)-q3*l2-q4*(l2**2)/2-q5*(l2**3)/3-q6*(l2**4)/4)
!pe_kp=2*3.1415/137*(1-1/(beta_k*n_kp2)**2)*(1/275.e-9 - 1/500.e-9)*0.2*0.65*0.5

! C4F10 parameters, with error corrected
q1o=-0.28521
q2o= 2.65338E+06
q3o=-2.83901E+12
q4o=-9.11288E+17
q5o=-7.34853E+24
q6o= 1.13191E+31
pef_c4f10=2*3.1415/137*(1-1/(beta_pi*n_pik2_c4f10)**2)*1.0*0.75
pe_pik_c4f10=pef_c4f10*(-q1o/l1+q2o*ln(l1)+q3o*l1+q4o*(l1**2)/2+q5o*(l1**3)/3+q6o*(l1**4)/4 +q1o/l2-q2o*ln(l2)-q3o*l2-q4o*(l2**2)/2-q5o*(l2**3)/3-q6o*(l2**4)/4)

label\yaxis `<pi><U>+<D> Photoelectrons'
scale 2.8 11 5 0 50 5
set xvmin 1
set xvmax 11
set ylog 0
set nsyinc 5

col 1
set lintyp 1
graph\axes
!col 7
!graph\noaxes p pe_pik_c4f10
col 1
graph\noaxes p pe_pik

! calculate the detection efficiency, assuming that there is an experimental
! cut at 1.5pe to eliminate knock-on events

i=0
do j=[0:30:0.1]
  i=i+1
  x[i]=j
enddo

scalar\dummy k

do i=[1:lenp]
  y=(pe_pik[i]**x)*(exp(-pe_pik[i]))/gamma(x+1)
  sumylo[i]=sum(y[k],k,1:15)/10
  sumy[i]=sum(y[k],k,1:len(y))/10
enddo
ineff_pik=1-(sumy-sumylo)/sumy*(1-exp(-pe_pik))

list p press_pik pe_pik ineff_pik

col 1
set %txthit 3.0
!col 7
!text `C<D>4<U>F<D>10'
col 1
text `C<D>4<U>F<D>8<U>O'
