! pik_n.pcm
! gh   02.03.20

enable echo
clear

m_pi=0.13957
m_k=0.49368
m_p=0.93827

i=0
do j=[0.5:14.0:0.1]
  i=i+1
  p[i]=j
enddo

beta_pi=p/sqrt(p**2+m_pi**2)
beta_k=p/sqrt(p**2+m_k**2)
beta_p=p/sqrt(p**2+m_p**2)

nmin_pi=1/beta_pi-1
nmin_k=1/beta_k-1
nmin_p=1/beta_p-1

! min n for pi
press_pi=(nmin_pi)/(1.00143-1)

! computations for pi/K separation
n_pikm=(nmin_k-nmin_pi)+nmin_pi+1
press_pikm=(n_pikm-1)/(1.00143-1)

! computations for K/p separation
n_kpm=(nmin_p-nmin_k)+nmin_k+1
press_kpm=(n_kpm-1)/(1.00143-1)

orientation landscape
label\xaxis `Momentum (GeV/c)'
set linthk 3
set ylog 10
set nsyinc 10
set %xlaxis 27
set nsxinc 2
set font triumf.2

window 1
label\yaxis `C<D>4<U>F<D>10<U> Pressure (atm)'
scale 3 11 4 -1.3 1.2 3
col 1
graph\axes

set lintyp 5
col 4
graph\noaxes p press_kpm
set lintyp 8
col 2
graph\noaxes p press_pikm
col 3
set lintyp 9
graph\noaxes p press_pi

! set limits consistent with good pi+/K+ separation
! Keep in mind that for a given SHMS Central P, we can have pi+ with P-15% 
! and K+ with P+25%
! the goal is that for these disparate momenta, the pressure is still 0.1 atm
! below the minimum pressure required for K+
! the 55% factor, below, seems to accomplish this.
!
!n_pik=(nmin_k-nmin_pi)*0.55+nmin_pi+1

pp25=p*1.25
pm15=p*0.85

beta_pi_m15=pm15/sqrt(pm15**2+m_pi**2)
beta_k_p25=pp25/sqrt(pp25**2+m_k**2)

nmin_pi_m15=1/beta_pi_m15-1
nmin_k_p25=1/beta_k_p25-1

press_pi_m15=(nmin_pi_m15)/(1.00143-1)
press_k_p25=(nmin_k_p25)/(1.00143-1)

press_pik=press_k_p25-0.1

! set limits consistent with the vapor pressure
lenp=len(p)
do  j=[1:lenp]
  if (press_pik[j]>0.95) then press_pik[j]=0.95
enddo

n_pik2=press_pik*(1.00143-1)+1
!n_kp2=press_kp*(1.00143-1)+1

col 1
set lintyp 1
graph\noaxes p press_pik

! calculate the number of P.E.'s taking into account
! the instrumentation wavelength characteristics via
!   q1 +q2*lambda +q3*(lambda**2) +q4*(lambda**3) +q5*(lambda**4) +p6*(lambda**5)
! where lambda is in m from 180 to 630 nm, and
q1= 0.56189
q2=-1.05897e07
q3= 6.64463e13
q4=-1.66962e20
q5= 1.80688e26
q6=-7.03064e31
! and assuming that 75% of the light is focussed onto the PMTs

pef=2*3.1415/137*(1-1/(beta_pi*n_pik2)**2)*0.70*0.75
l1=630.e-9
l2=180.e-9

pe_pik=pef*(-q1/l1+q2*ln(l1)+q3*l1+q4*(l1**2)/2+q5*(l1**3)/3+q6*(l1**4)/4 +q1/l2-q2*ln(l2)-q3*l2-q4*(l2**2)/2-q5*(l2**3)/3-q6*(l2**4)/4)

!pe_kp=2*3.1415/137*(1-1/(beta_k*n_kp2)**2)*(1/275.e-9 - 1/500.e-9)*0.2*0.65*0.5

window 2
label\yaxis `<pi><U>+<D> Photoelectrons'
scale 3 11 4 5 25 4
set ylog 0
set nsyinc 5

col 1
set lintyp 1
graph p pe_pik

! calculate the detection efficiency, assuming that there is an experimental
! cut at 1.5pe to eliminate knock-on events

i=0
do j=[0:30:0.1]
  i=i+1
  x[i]=j
enddo

scalar\dummy k

do i=[1:lenp]
  y=(pe_pik[i]**x)*(exp(-pe_pik[i]))/gamma(x+1)
  sumylo[i]=sum(y[k],k,1:15)/10
  sumy[i]=sum(y[k],k,1:len(y))/10
enddo
eff_pik=1-(sumy-sumylo)/sumy*(1-exp(-pe_pik))

list p press_pik press_pikm eff_pik

col 1
window 0
set %txthit 3.0
col 4
text `p'
col 2
text `K<U>+'
col 1
text `set'
col 3
text `<pi><U>+'
